<?xml version="1.0" encoding="utf-8"?>
<stylesheet id="stylV5ta5nBIWJUZ" lang="any" show-in="main" webpalschematype="wpAppStylesheet"><![CDATA[<?xml version="1.0"?>
<!DOCTYPE xsl:stylesheet [
<!ENTITY nbsp "&#160;" >
]>

<!-- (c) 2001-present All Copyrights Palomino System Innovations Inc.
Illegal reproduction and disclosure will be prosecuted  -->

<xsl:stylesheet
version="1.0"
xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
xmlns:wp="http://www.palominosys.com/wp"
xmlns:php="http://php.net/xsl"
xmlns:date="http://exslt.org/dates-and-times"
xmlns:exsl="http://exslt.org/common"
extension-element-prefixes="exsl"
>

  <!-- ========================================================================================= -->
  <!-- =================================== newslist templates ================================== -->
  <!-- ========================================================================================= -->

  <xsl:template match="newslist">
	<!-- variable definitions -->

	<xsl:variable name="max-items">
	  <xsl:choose>
		<xsl:when test="./@index='10-most-recent'"><xsl:value-of select="number(10)"/></xsl:when>
		<xsl:when test="./@index='5-most-recent'"><xsl:value-of select="number(5)"/></xsl:when>
		<xsl:when test="./@index='most-recent'"><xsl:value-of select="number(1)"/></xsl:when>
		<xsl:when test="./@index='list-none'"><xsl:value-of select="number(0)"/></xsl:when>
		<xsl:when test="./@index='breaking-news-only'"><xsl:value-of select="count(news[@priority='breaking-news'])"/></xsl:when>
		<xsl:when test="./@index='frontpage-only'"><xsl:value-of select="count(news[@priority='frontpage'])"/></xsl:when>
		<xsl:otherwise><xsl:value-of select="count(news)"/></xsl:otherwise>
	  </xsl:choose>
	</xsl:variable>

	<!-- get the path for each page link -->

	<xsl:variable name="chapter_id" select="../@id"/>
	<xsl:variable name="path">
	  <xsl:call-template name="create_xpath_to_node">
		<xsl:with-param name="node_id" select="$chapter_id"/>
		<xsl:with-param name="web_root" select="//pages"/>
	  </xsl:call-template>
	</xsl:variable>
	<!--<xsl:variable name="news-base-link" select="concat('?path=',$path)"/>-->
	<xsl:variable name="news-base-link">
	  <xsl:choose>
		<xsl:when test="./@enable-individual-page='no' or ./enable-individual-page=''">
		  <xsl:value-of select="''"/>
		</xsl:when>
		<xsl:otherwise>
<!-- 		  <xsl:value-of select="concat('?path=',$path)"/> -->
          <xsl:value-of select="'/our-company/investors'"/>
		</xsl:otherwise>
	  </xsl:choose>
	</xsl:variable>

	<!-- end of variable definitions -->

	<!-- get a new newslist node with new attributes for easier styling -->
	<xsl:variable name="new-newslist-tree">
	  <xsl:apply-templates select="." mode="pre-process">
		<xsl:with-param name="max-items" select="$max-items"/>
		<xsl:with-param name="index" select="./@index"/>
		<xsl:with-param name="news-base-link" select="$news-base-link"/>
	  </xsl:apply-templates>
	</xsl:variable>

	<xsl:choose>
	  <xsl:when test="$max-items=0">
		<!-- no news avail. or no items allowed -->
		<xsl:text disable-output-escaping="yes">&lt;div class="message"&gt;There is no item at the moment.&lt;/div&gt;</xsl:text>
	  </xsl:when>
	  <xsl:otherwise>
		<!-- render page -->
		<xsl:variable name="new-newslist" select="exsl:node-set($new-newslist-tree)/newslist"/>
		<!-- get attribute name for sorting -->
		<xsl:variable name="sort-by">
		  <xsl:choose>
			<xsl:when test="$new-newslist/news[1]/@*[name()!=@index-sort]"><xsl:value-of select="'date'"/></xsl:when>
			<xsl:otherwise><xsl:value-of select="$new-newslist/@index-sort"/></xsl:otherwise>
		  </xsl:choose>
		</xsl:variable>

		<xsl:text disable-output-escaping="yes">&lt;ul id="</xsl:text>
		<xsl:value-of select="$new-newslist/@name"/>
		<xsl:text disable-output-escaping="yes">" style="list-style:none;padding-left:0px;"&gt;</xsl:text>

		<xsl:choose>
		  <xsl:when test="$new-newslist/@index='breaking-news-first'">
			<!--
				when 'breaking-news-first' selected,
				add sorting by priority number as an extra sort criteria
			-->
			<xsl:apply-templates select="$new-newslist/news" mode="page">
			  <xsl:sort order="descending" select="@priority-number"/>
			  <xsl:sort order="{@sort-order}" select="@*[name()=$sort-by]"/>
			  <xsl:with-param name="max-items" select="$new-newslist/@max-items"/>
			  <xsl:with-param name="items-per-page" select="$new-newslist/@items-per-page"/>
			  <xsl:with-param name="number-of-pages" select="ceiling(count($new-newslist/news) div $new-newslist/@items-per-page)"/>
			  <xsl:with-param name="layout" select="$new-newslist/@layout"/>
			  <xsl:with-param name="date-format" select="$new-newslist/@date-format"/>
			</xsl:apply-templates>
		  </xsl:when>
		  <xsl:otherwise>
			<xsl:apply-templates select="$new-newslist/news" mode="page">
			  <xsl:sort order="{@sort-order}" select="@*[name()=$sort-by]"/>
			  <xsl:with-param name="max-items" select="$new-newslist/@max-items"/>
			  <xsl:with-param name="items-per-page" select="$new-newslist/@items-per-page"/>
			  <xsl:with-param name="number-of-pages" select="ceiling(count($new-newslist/news) div $new-newslist/@items-per-page)"/>
			  <xsl:with-param name="layout" select="$new-newslist/@layout"/>
			  <xsl:with-param name="date-format" select="$new-newslist/@date-format"/>
			</xsl:apply-templates>
		  </xsl:otherwise>
		</xsl:choose>

		<xsl:text disable-output-escaping="yes">&lt;/ul&gt;</xsl:text>
		<!-- <div class="pagination compact-theme simple-pagination"></div> -->

		<xsl:variable name="containerID" select="concat('&quot;',$new-newslist/@name,'&quot;')"/>

	  </xsl:otherwise>
	</xsl:choose>

  </xsl:template>

  <!-- ========================================================================================= -->
  <!-- ================================= newslist page templates =============================== -->
  <!-- ========================================================================================= -->

  <xsl:template match="news" mode="page">
	<xsl:param name="max-items"/>
	<xsl:param name="items-per-page"/>
	<xsl:param name="number-of-pages"/>
	<xsl:param name="layout"/>
	<xsl:param name="date-format"/>

<!-- 	<xsl:if test="position() &lt;= $max-items"> -->
    <xsl:if test="position() &lt;= $max-items">
	  <!--
	  <xsl:if test="position() mod $items-per-page=1">
		Page
		<hr />
	  </xsl:if>
	  -->
	  <li>
      <!-- title here -->
	  <p class="meta-title">
		<xsl:if test="./@link">
		  <xsl:text disable-output-escaping="yes">&lt;a href="</xsl:text>
		  <xsl:value-of select="./@link"/>
		  <xsl:text disable-output-escaping="yes">" rel="bookmark" title="</xsl:text>
		  <xsl:value-of select="title"/>
		  <xsl:text disable-output-escaping="yes">"&gt;</xsl:text>
		</xsl:if>
		<xsl:if test="contains($layout,'date')">
		  <xsl:variable name="formatted-date">
			<xsl:call-template name="format-date">
			  <xsl:with-param name="format" select="$date-format"/>
			  <xsl:with-param name="date" select="./@date"/>
			</xsl:call-template>
		  </xsl:variable>
		  <xsl:value-of select="$formatted-date"/>
		</xsl:if>
		<xsl:value-of select="concat(' ',title)"/>
		<xsl:if test="./@link">
		  <xsl:text disable-output-escaping="yes">&lt;/a&gt;</xsl:text>
		</xsl:if>
	  </p>

	  <div class="meta meta-date">
		<!-- date here -->
	  </div>

	  <div class="meta meta-author">
		<!-- author here -->
		<xsl:if test="author">
		  <xsl:value-of select="author"/>
		</xsl:if>
	  </div>

	  <!-- description here -->
	  <xsl:if test="contains($layout,'synopsis')">
		<div class="entry-synopsis"><xsl:value-of select="synopsis"/><xsl:text disable-output-escaping="yes"> </xsl:text>
		<xsl:if test="./@link">
		  <xsl:text disable-output-escaping="yes">&lt;a href="</xsl:text>
		  <xsl:value-of select="./@link"/>
		  <xsl:text disable-output-escaping="yes">" rel="bookmark" title="Click here to view full news" class="newsReadMore"&gt;Read more &lt;i class="fa fa-angle-double-right"&gt;&lt;/i&gt; &lt;/a&gt;</xsl:text>
		</xsl:if>
		</div>
	  </xsl:if>


	  <!-- content here -->
	  <xsl:if test="contains($layout,'text')">
		<div class="entry"><xsl:apply-templates select="text" /></div>
	  </xsl:if>
	  <!--
	  <div>content: <xsl:value-of select="text"/></div>
	  <div>post-live-at: <xsl:value-of select="./@post-live-at"/></div>
	  <div>expire-after: <xsl:value-of select="./@expire-after"/></div>
	  -->
	  <!--<hr />-->
	  </li>

	  <!--
	  <xsl:if test="position() mod $items-per-page=0 or position()=$max-items">
		END
		<br />
	  </xsl:if>
	  -->
	</xsl:if>
  </xsl:template>

  <!-- ========================================================================================= -->
  <!-- ============================ Individual news page template ============================== -->
  <!-- ========================================================================================= -->

  <xsl:template match="newslist/news">

	<xsl:variable name="news-tree">
	  <xsl:apply-templates select="." mode="pre-process">
		<xsl:with-param name="index" select="../@index"/>
		<xsl:with-param name="news-base-link" select="''"/>
	  </xsl:apply-templates>
	</xsl:variable>

	<xsl:variable name="news" select="exsl:node-set($news-tree)/news"/>
    <div class="columns">
      <div class="column is-2"></div>
      <div class="py-5 column">
      <h2>
        <xsl:value-of select="$news/title"/>
      </h2>

      <div class="meta">
        <!-- date here -->
        <xsl:if test="contains(../@layout,'date')">
          <xsl:variable name="formatted-date">
            <xsl:call-template name="format-date">
              <xsl:with-param name="format" select="../@date-format"/>
              <xsl:with-param name="date" select="$news/@date"/>
            </xsl:call-template>
          </xsl:variable>
          <i class="icon-calendar"></i><xsl:value-of select="$formatted-date"/>
        </xsl:if>
        <!-- author here -->
        <xsl:if test="$news/author">
          <i class="icon-user"></i><xsl:value-of select="$news/author"/>
        </xsl:if>
      </div>

      <!-- content here -->
      <div class="entry">
        <xsl:apply-templates select="$news/text"/>

        <xsl:variable name="addThis" select="../@addThis-ID"/>
        <xsl:variable name="showSocialMedia" select="../@email-to-friend" />
        <xsl:if test="$showSocialMedia = 'yes'">

          <script type="text/javascript" src="//s7.addthis.com/js/300/addthis_widget.js#pubid={$addThis}"></script>
          <div class="addthis_toolbox addthis_default_style  addthis_32x32_style">
            <a class="addthis_button_email"></a>
          </div>
        </xsl:if>
      </div>

      </div>
      <div class="column is-2"></div>
    </div>
  </xsl:template>

  <!-- ========================================================================================= -->
  <!-- ================================= pre-process templates ================================= -->
  <!-- ========================================================================================= -->

  <!-- pre-process newslist -->
  <xsl:template match="newslist" mode="pre-process">
	<xsl:param name="max-items"/>
	<xsl:param name="index"/>
	<xsl:param name="news-base-link"/>
	<xsl:param name="get-archived"/>

	<xsl:copy>
      <xsl:attribute name="max-items">
        <xsl:value-of select="$max-items"/>
      </xsl:attribute>
      <xsl:apply-templates select="@*|node()" mode="pre-process">
		<xsl:with-param name="index" select="$index"/>
		<xsl:with-param name="news-base-link" select="$news-base-link"/>
		<xsl:with-param name="get-archived" select="$get-archived"/>
	  </xsl:apply-templates>
    </xsl:copy>
  </xsl:template>

  <!-- pre-process news -->
  <xsl:template match="news" mode="pre-process">
	<xsl:param name="index"/>
	<xsl:param name="news-base-link"/>
	<xsl:param name="get-archived" />

	<!--
	<xsl:variable name="pla-time" select="php:function('strtotime',string(./@post-live-at))"/>
	<xsl:variable name="ea-time" select="php:function('strtotime',string(./@expire-after))"/>
	-->
	<xsl:variable name="current-day" select="php:function('strtotime','today')"/>
	<xsl:variable name="pla-time">
	  <xsl:choose>
		<xsl:when test="./@post-live-at='' or not(./@post-live-at)">
		  <xsl:value-of select="php:function('strtotime','1990-01-01')"/>
		</xsl:when>
		<xsl:otherwise>
		  <xsl:value-of select="php:function('strtotime',string(./@post-live-at))"/>
		</xsl:otherwise>
	  </xsl:choose>
	</xsl:variable>
	<xsl:variable name="ea-time">
	  <xsl:choose>
		<xsl:when test="./@expire-after='' or not(./@expire-after)">
		  <xsl:value-of select="php:function('strtotime','2099-12-31')"/>
		</xsl:when>
		<xsl:otherwise>
		  <xsl:value-of select="php:function('strtotime',string(./@expire-after))"/>
		</xsl:otherwise>
	  </xsl:choose>
	</xsl:variable>

	<!-- <xsl:variable name="date-validate" select="$pla-time &lt;= $current-day and $current-day &lt;= $ea-time"/>-->

	<!-- copy breaking news or frontpage only if one of these two options is flagged -->
	<!-- copy non-expired news only -->
	<xsl:if test="(
				   $index!='breaking-news-only'
				   or ($index='breaking-news-only' and @priority='breaking-news')
				   or ($index='frontpage-only' and @priority='frontpage')
				  )
				  and
				  (
				   ($get-archived and $ea-time &lt; $current-day)
				    or (not($get-archived) and ($pla-time &lt;= $current-day and $current-day &lt;= $ea-time))
				  )">
	  <xsl:copy>
		<xsl:attribute name="priority-number">
		  <xsl:choose>
			<xsl:when test="@priority='breaking-news'">
			  <xsl:value-of select="3"/>
			</xsl:when>
			<xsl:when test="@priority='frontpage'">
			  <xsl:value-of select="2"/>
			</xsl:when>
			<xsl:when test="@priority='regular'">
			  <xsl:value-of select="1"/>
			</xsl:when>
		  </xsl:choose>
		</xsl:attribute>

		<xsl:if test="$news-base-link and $news-base-link != '' and @name and @name != ''">
		  <xsl:attribute name="link">
            <xsl:choose>
              <xsl:when test="contains($news-base-link, '?')">
                <xsl:value-of select="concat($news-base-link,'&amp;node=',@name)"/>
              </xsl:when>
              <xsl:otherwise>
                <xsl:value-of select="concat($news-base-link,'?node=',@name)"/>
              </xsl:otherwise>
            </xsl:choose>
		  </xsl:attribute>
		</xsl:if>

		<xsl:apply-templates select="@*|node()" mode="pre-process"/>
	  </xsl:copy>
	</xsl:if>
  </xsl:template>

  <!-- pre-process all other nodes (copy forward) -->
  <xsl:template match="@*|node()" mode="pre-process">
	<xsl:choose>
	  <!-- parse whitespace properly -->
	  <xsl:when test="local-name()='ch' and ./@value='nbsp'">
		<xsl:text disable-output-escaping="yes"> </xsl:text>
	  </xsl:when>
	  <xsl:otherwise>
		<xsl:copy>
		  <xsl:apply-templates select="@*|node()" mode="pre-process"/>
		</xsl:copy>
	  </xsl:otherwise>
	</xsl:choose>
  </xsl:template>

  <!-- ========================================================================================= -->
  <!-- ======================================== functions ====================================== -->
  <!-- ========================================================================================= -->

  <xsl:template name="format-date">
	<xsl:param name="format"/>
	<xsl:param name="date"/>

	<xsl:variable name="php-format-string">
	  <xsl:choose>
		<xsl:when test="$format='yyyy-mm-dd'">
		  <xsl:value-of select="'Y-m-d'"/>
		</xsl:when>
		<xsl:when test="$format='dd-mm-yyyy'">
		  <xsl:value-of select="'d-m-Y'"/>
		</xsl:when>
		<xsl:when test="$format='yyyy/mm/dd'">
		  <xsl:value-of select="'Y/m/d'"/>
		</xsl:when>
		<xsl:when test="$format='mm/dd/yyyy'">
		  <xsl:value-of select="'m/d/Y'"/>
		</xsl:when>
		<xsl:when test="$format='dd/mm/yyyy'">
		  <xsl:value-of select="'d/m/Y'"/>
		</xsl:when>
		<xsl:when test="$format='dd-Mon-yy'">
		  <xsl:value-of select="'d-M-y'"/>
		</xsl:when>
		<xsl:when test="$format='Weekday_Month_dd_yyyy'">
		  <xsl:value-of select="'l F d Y'"/>
		</xsl:when>
		<xsl:when test="$format='Weekday_dd_Month_yyyy'">
		  <xsl:value-of select="'l d F Y'"/>
		</xsl:when>
		<xsl:when test="$format='Wkd_Mon_dd_yyyy'">
		  <xsl:value-of select="'D M d Y'"/>
		</xsl:when>
		<xsl:when test="$format='Wkd_Mon_dd_yy'">
		  <xsl:value-of select="'D M d y'"/>
		</xsl:when>
		<xsl:when test="$format='Month_dd_yyyy'">
		  <xsl:value-of select="'F d Y'"/>
		</xsl:when>
		<xsl:when test="$format='Mon_dd_yyyy'">
		  <xsl:value-of select="'M d Y'"/>
		</xsl:when>
	  </xsl:choose>
	</xsl:variable>

	<xsl:variable name="date-string" select="php:function('strtotime',string($date))"/>
	<xsl:value-of select="php:function('date',$php-format-string,number($date-string))"/>
  </xsl:template>

  <xsl:template name="create_xpath_to_node">
	<xsl:param name="node_id"/>
	<xsl:param name="web_root"/>
	<xsl:variable name="node" select="$web_root//page[@id=$node_id]"/>
	<xsl:variable name="ancestors" select="$node/ancestor::page"/>
	<xsl:value-of select="string('/')"/>
	<xsl:for-each select="$ancestors">
	  <xsl:value-of select="@name"/>
	  <xsl:value-of select="string('/')"/>
	</xsl:for-each>
	<xsl:value-of select="$node/@name"/>
  </xsl:template>


</xsl:stylesheet>]]></stylesheet>
